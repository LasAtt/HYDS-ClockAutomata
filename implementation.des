INITIALIZER:
    from Tkinter import *
    from DWatchGUI import DWatchGUI
    from AsyncUpdater import AsyncUpdater

    root = Tk()
    root.title("Product of Texas")
    g = DWatchGUI(root, eventhandler)

    def checkAlarm():
      if g.checkTime():
        g.alarmStart()

    eventhandler.start()
    root.mainloop()

STATECHART:
    CLOCK [CS] [DS]
        ON [DS]
        OFF
    CHRONO [CS] [DS]
        OFF [DS]
        ON
    LIGHTS [CS] [DS]
        OFF [DS]
        ON
        T
    ATIME [CS] [DS]
        OFF [DS]
        ON
    MAIN [CS] [DS]
        A
            Y [DS]
            N
        MAIN [DS] [HS*]
            N [DS]
            CHRONO
            AH
            TH
            E
                D [DS]
                LH
                RH
                I


TRANSITION:
    S: MAIN.MAIN.N
    N: MAIN.MAIN.N
    T: 0.5
    O: g.refreshTimeDisplay()

TRANSITION:
    S: MAIN.MAIN
    N: MAIN.A.Y
    E: alarming
    O: g.setIndiglo()

TRANSITION:
    S: MAIN.A.Y
    N: MAIN.A.N
    T: 0.25
    O: g.unsetIndiglo()

TRANSITION:
    S: MAIN.A.N
    N: MAIN.A.Y
    T: 0.25
    O: g.setIndiglo()

TRANSITION: [HS]
    S: MAIN.A
    N: MAIN.MAIN
    T: 4.5
    O: g.unsetIndiglo()

TRANSITION: [HS]
    S: MAIN.A
    N: MAIN.MAIN
    E: lightOn
    O: g.unsetIndiglo()

TRANSITION: [HS]
    S: MAIN.A
    N: MAIN.MAIN
    E: changeMode
    O: g.unsetIndiglo()

TRANSITION: [HS]
    S: MAIN.A
    N: MAIN.MAIN
    E: initChrono
    O: g.unsetIndiglo()

TRANSITION: [HS]
    S: MAIN.A
    N: MAIN.MAIN
    E: resetChrono
    O: g.unsetIndiglo()

#Edit alarm

TRANSITION:
    S: MAIN.MAIN.N
    N: MAIN.MAIN.AH
    E: setAlarm
    O: g.refreshAlarmDisplay()

TRANSITION:
    S: MAIN.MAIN.AH
    N: MAIN.MAIN.E.D
    T: 1.5
    O: g.startSelection()

TRANSITION:
    S: MAIN.MAIN.AH
    N: MAIN.MAIN.N
    E: onoff
    O: eventhandler.event("toggleAlarm")

#Edit time

TRANSITION:
    S: MAIN.MAIN.N
    N: MAIN.MAIN.TH
    E: editTime
    O: print "button pressed"

TRANSITION:
    S: MAIN.MAIN.TH
    N: MAIN.MAIN.N
    E: released
    O: print "button released"

TRANSITION:
    S: MAIN.MAIN.TH
    N: MAIN.MAIN.E
    T: 1.5
    O: eventhandler.event("clockOff")
       g.startSelection()

#Shared selection edit

TRANSITION:
    S: MAIN.MAIN.E.D
    N: MAIN.MAIN.N
    T: 5
    O: g.stopSelection()
       eventhandler.event("clockOn")

TRANSITION:
    S: MAIN.MAIN.E.D
    N: MAIN.MAIN.E.LH
    E: increase
    O: print "increase pressed"

TRANSITION:
    S: MAIN.MAIN.E.LH
    N: MAIN.MAIN.E.D
    E: stopInc
    O: g.increaseSelection()

TRANSITION:
    S: MAIN.MAIN.E.LH
    N: MAIN.MAIN.E.I
    T: 0.3
    O: g.increaseSelection()

TRANSITION:
    S: MAIN.MAIN.E.I
    N: MAIN.MAIN.E.I
    T: 0.3
    O: g.increaseSelection()

TRANSITION:
    S: MAIN.MAIN.E.I
    N: MAIN.MAIN.E.D
    E: stopInc
    O: print "increase stopped"

TRANSITION:
    S: MAIN.MAIN.E.D
    N: MAIN.MAIN.E.RH
    E: finishEdit
    O: print "edit finished"

TRANSITION:
    S: MAIN.MAIN.E.RH
    N: MAIN.MAIN.N
    T: 2
    O: g.stopSelection()
       eventhandler.event("clockOn")

TRANSITION:
    S: MAIN.MAIN.E.RH
    N: MAIN.MAIN.E.D
    E: released
    O: g.selectNext()

#Lights

TRANSITION:
    S: LIGHTS.OFF
    N: LIGHTS.ON
    E: lightOn
    O: g.setIndiglo()

TRANSITION:
    S: LIGHTS.ON
    N: LIGHTS.T
    E: lightOff
    O: print "light turning off"

TRANSITION:
    S: LIGHTS.T
    N: LIGHTS.OFF
    T: 2
    O: g.unsetIndiglo()

#Clock

TRANSITION:
    S: CLOCK.OFF
    N: CLOCK.ON
    E: clockOn
    O: print "clock on"

TRANSITION:
    S: CLOCK.ON
    N: CLOCK.OFF
    E: clockOff
    O: print "clock off"

TRANSITION: [HS]
    S: CLOCK.ON
    N: CLOCK.ON
    T: 1
    O: g.increaseTimeByOne()

#Chrono

TRANSITION:
    S: CHRONO.OFF
    N: CHRONO.ON
    E: toggleChrono
    O: print "chrono on"

TRANSITION:
    S: CHRONO.ON
    N: CHRONO.OFF
    E: toggleChrono
    O: print "chrono on"

TRANSITION:
    S: CHRONO.ON
    N: CHRONO.ON
    T: 0.25
    O: g.increaseChronoByOne()

#Alarm

TRANSITION:
    S: ATIME.ON
    N: ATIME.OFF
    E: toggleAlarm
    O: g.setAlarm()

TRANSITION:
    S: ATIME.OFF
    N: ATIME.ON
    E: toggleAlarm
    O: g.setAlarm()

TRANSITION:
    S: ATIME.ON
    N: ATIME.ON
    T: 0.5
    O: checkAlarm()

#Chrono view

TRANSITION:
    S: MAIN.MAIN.N
    N: MAIN.MAIN.CHRONO
    E: changeMode
    O: g.refreshChronoDisplay()

TRANSITION:
    S: MAIN.MAIN.CHRONO
    N: MAIN.MAIN.N
    E: changeMode
    O: g.refreshTimeDisplay()

TRANSITION:
    S: MAIN.MAIN.CHRONO
    N: MAIN.MAIN.CHRONO
    E: initChrono
    O: eventhandler.event("toggleChrono")

TRANSITION:
    S: MAIN.MAIN.CHRONO
    N: MAIN.MAIN.CHRONO
    E: resetChrono
    O: g.resetChrono()

TRANSITION:
    S: MAIN.MAIN.CHRONO
    N: MAIN.MAIN.CHRONO
    T: 0.1
    O: g.refreshChronoDisplay()
